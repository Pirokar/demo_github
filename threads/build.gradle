plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'maven-publish'
    id 'kotlin-allopen'
}

Properties properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(rootProject.file("local.properties").newDataInputStream())
}
def mavenProdUser = properties.getProperty('mavenProdUser')
def mavenProdPass = properties.getProperty('mavenProdPass')

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    compileSdk rootProject.compileSdkVersion

    lintOptions {
        disable 'InvalidPackage'
    }

    buildFeatures {
        dataBinding = true
    }

    defaultConfig {
        minSdk rootProject.minSdkVersion
        targetSdk rootProject.targetSdkVersion

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            buildConfigField "String", "VERSION_NAME", "\"${version}\""
            consumerProguardFiles 'proguard-rules.pro'
        }
        debug {
            buildConfigField "String", "VERSION_NAME", "\"${version}\""
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildFeatures {
        viewBinding = true
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                artifact "build/outputs/aar/threads-release.aar"
                groupId = 'im.threads'
                artifactId = 'threads-release'
                version = "${version}"
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        dependencyNode.appendNode('scope', 'runtime')
                    }
                }
            }
            debug(MavenPublication) {
                artifact "build/outputs/aar/threads-debug.aar"
                groupId = 'im.threads'
                artifactId = 'threads-debug'
                version = "${version}"
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        dependencyNode.appendNode('scope', 'runtime')
                    }
                }
            }
        }

        repositories {
            maven {
                if ("$version".contains('SNAPSHOT')) {
                    // dev
                    url 'https://maven-pub.edna.ru/repository/maven-snapshots'
                } else {
                    // release
                    url 'https://maven-pub.edna.ru/repository/maven-releases'
                }
                credentials {
                    username "$mavenProdUser"
                    password "$mavenProdPass"
                }
            }
        }
    }
}

dependencies {

    implementation "androidx.preference:preference-ktx:$rootProject.androidXPreferenceVersion"
    implementation "androidx.core:core-ktx:$rootProject.androidXCoreVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$rootProject.androidXSwiperefreshlayout"
    implementation "com.google.android.material:material:$rootProject.materialComponentsVersion"

    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"

    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttpVersion"

    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxjava2Version"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroidVersion"
    implementation "com.annimon:stream:$rootProject.streamVersion"

    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"

    implementation "com.github.3llomi:RecordView:$rootProject.recordViewVersion"
    implementation "com.writingminds:FFmpegAndroid:$ffmpeg"

    implementation "com.google.firebase:firebase-messaging:$fcmVersion"
    implementation "androidx.security:security-crypto:$crypto"

    implementation "com.google.android.flexbox:flexbox:$flexboxVersion"

    implementation("androidx.work:work-runtime-ktx:$work_version")

    implementation "net.zetetic:android-database-sqlcipher:$sqlcipher_version"
    implementation "androidx.sqlite:sqlite:$sqlite_version"

    //Markwon
    implementation "io.noties.markwon:core:$markwonVersion"
    implementation "io.noties.markwon:linkify:$markwonVersion"
    implementation "io.noties.markwon:ext-strikethrough:$markwonVersion"

    //Coil
    implementation "io.coil-kt:coil:$coil_version"

    testImplementation "org.robolectric:robolectric:$rootProject.robolectricVersion"
    testImplementation "androidx.test.ext:junit:$rootProject.androidXJunitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: rootProject.mockitoInlineVersion
    androidTestImplementation "androidx.test:runner:$rootProject.androidXTestVersion"
    androidTestImplementation "androidx.test:rules:$rootProject.androidXTestVersion"
    androidTestImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    androidTestImplementation "org.mockito:mockito-android:$rootProject.mockitoVersion"
}

allOpen {
    annotation('im.threads.annotation.OpenClass')
}
